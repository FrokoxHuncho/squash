TARGET = squash
QT = core gui
CONFIG -= app_bundle lib_bundle ppc
#-------------------------------------------------------------------------------

SOURCES      = src/ImageLoader.cpp \
	       src/ImageResizer.cpp \
	       src/ImagesModel.cpp \
	       src/ImagesView.cpp \
	       src/main.cpp \
	       src/SquashWindow.cpp
HEADERS      = src/ImageLoader.h \
	       src/ImageResizer.h \
	       src/ImagesModel.h \
	       src/ImagesView.h \
	       src/SquashWindow.h
RESOURCES    = qrc/resources.qrc
TRANSLATIONS = tr/squash_en.ts

#-------------------------------------------------------------------------------

OBJECTS_DIR = build
MOC_DIR = build
UI_DIR = build

# precompiled headers breaks icecream builds for some reason :(
#unix:system( test `ps aux | grep iceccd | wc -l` -gt 1 ): CONFIG -= precompile_header
#-------------------------------------------------------------------------------

debug {
	QMAKE_CXXFLAGS_DEBUG = -ggdb
}
#-------------------------------------------------------------------------------

release {
    linux* {
        QMAKE_CXXFLAGS_RELEASE = -O3 -freorder-blocks -fno-reorder-functions -fomit-frame-pointer -gstabs+
    }
    mac* {
        QMAKE_CXXFLAGS_RELEASE = -O3 -g -fomit-frame-pointer
    }
    win32-msvc {
        QMAKE_CXXFLAGS_RELEASE = -Zi -Oy -Ox -GL -MD
        QMAKE_LFLAGS_RELEASE = /ltcg
    }
    win32-g++ {
        QMAKE_CXXFLAGS_RELEASE = -O3 -freorder-blocks -fno-reorder-functions -fomit-frame-pointer -gstabs+
    }
    
#    QMAKE_CXXFLAGS_RELEASE += -DNDEBUG
	CONFIG += warn_off
}
#-------------------------------------------------------------------------------
